plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.sourya.api'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

processResources {
    dependsOn(generateSwaggerCode)
}

swaggerSources {
    def typeMappings = 'URI=URI'
    def importMappings = 'URI=java.net.URI'
    eStore {
        def apiYaml = "${rootDir}/src/main/resources/api/openapi.yaml"
        def configJson= "${rootDir}/src/main/resources/api/config.json"
        inputFile = file(apiYaml)
        def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")
        code {
            language = 'spring'
            configFile = file(configJson)
            rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings',
                        typeMappings, '--import-mappings', importMappings] as List<String>
            components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
            dependsOn validation
        }
    }
}

compileJava.dependsOn swaggerSources.eStore.code
sourceSets.main.java.srcDir "${swaggerSources.eStore.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.eStore.code.outputDir}/src/main/java"


repositories {
	mavenCentral()
    maven{ url 'https://repo.spring.io/milestone' }
}

dependencies {
	swaggerCodegen 'org.openapitools:openapi-generator-cli:6.2.1'
	compileOnly 'io.swagger:swagger-annotations:1.6.4'
	compileOnly 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.openapitools:jackson-databind-nullable:0.2.3'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'io.springfox:springfox-oas:3.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'com.h2database:h2'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    jvmArgs '--enable-preview'
    useJUnitPlatform()
}

bootJar {
    manifest {
        attributes 'Implementation-Version': "${version}"
        attributes 'Implementation-Title': "${project.name}"
        attributes 'Manifest-Version': "1.0"
    }
}
